;links between firms and households for consumption
undirected-link-breed [consumer-links consumer-link]

;links between firms and households for employment
undirected-link-breed [employment-links employment-link]

undirected-link-breed [equity-links equity-link]

;link between firms and other firms for supply distribution.
directed-link-breed [framework-agreements framework-agreement]

;defines consumers investment within a certain firm, equity links remain even if a household changes employment
equity-links-own [
  equity                           ; represents what perent of a firm is owned by a household
]

;defines consumer link specific fields
consumer-links-own [
  demand-not-satisfied             ; the amount that a firm was unable to satisfy when asked by a household
]

;defines framework-agreement specific fields
framework-agreements-own[
  demand-not-satisfied
  index-multiplier                 ; relating index to price, used to find price from a given index
  index-type                       ; identifies which index the framework agreement is based on, index-type = 0 indicates price should be used 
  expiration-date                  ; tick when framework-agreement will expire
]

;defines all global variables, see "setup-constants" for a description of what each of these does
globals [
  CONSUMER-GOOD-FIRMS              ; defined as firms that produce goods purchased by households           
  PRIMARY-GOOD-FIRMS               ; defined as firms that produce goods without needing an input                 
  INTERMEDIATE-GOOD-FIRMS          ; defined as firms that do not produce consumer goods and take input             
  TOTAL-BANKRUPT-FIRMS             ; defined as the total number of firms bankrupt in the model
  LAST-TWO-YEARS-PRIMARY-INVENTORY ; defined as the rolling average of the total inventory of the last two years of the primary good firms
  ALL-FIRM-PROFITS                 ; list of all lifetime profits from firms. A "profit" is added to the list if the firm goes bankrupt and all active firms are added at the end of the month
  BANKRUPT-FIRM-PROFITS            ; identical to all-firm-profits less the active firms
  TOTAL-REVENUE                    ; total-revenue is a number representing the total revenue generated by firms
  UNEMPLOYMENT-RATES               ; stores a list of historical data for unemployment rates
  MEAN-PRICES                      ; stores a list of historical data for the mean price every year
  
  MIN-WAGE-RATE                    ; max amount wages are increased or decreased by if firm decides to change wage
  FIRM-STRUCTURE                   ; global data holding how many firms are initialized
  MAX-WAGE-GROWTH                  ; defined as the max amount a firm can increase or decrease their wage if they decide to change their wage
  MONTHS-TO-LOWER-WAGE             ; after this many months of having all positions filled, a firm will decrease wages
  MAX-PRICE-GROWTH                 ; defined as the max amount a firm increases/decreases prices by
  BUFFER-LABOR-FRACTION            ; the fraction of labor costs to keep as a buffer
  DIMINISHING-UTILITY-CONSTANT     ; the decay rate of how much wealth is used on consumption by households, used in consumption function
  PROB-REPLACE-FIRM-PRICE          ; the probability of switching trading firms to a new firm with a better price
  PROB-REPLACE-FIRM-QUANT          ; the probability of switching trading firms based on lack of quantity
  N-TRADING-LINKS                  ; number of firms contacted by consumers trying to buy consumer goods
  N-FRAMEWORK-AGREEMENTS           ; number of firms contacted by non-primary firms trying to buy input goods
  AUTONOMOUS-CONSUMPTION           ; represents amount in goods of autonomous consumption among households, regardless of wage
  STARTUP-LIQUIDITY                ; agents are initialized to a constant liquidity
  BACKGROUND-IMPROVEMENT           ; the background improvement on each patch after each tick 
  FIRM-MEMORY-CONSTANT             ; how much weighting a firm will put on current information vs. previous information
  
  month                            ; the current tick
  pringle-index-value              ; index id: 1
  coats-index-value                ; index id: 2
  ussher-index-value               ; index id: 3
  potvin-index-value               ; index id: 4
]

;general setup procedure, calls all other sub-setup procedures in some capacity to generate an initial state for the model
to setup
  ca
  
  set-constants
  set TOTAL-BANKRUPT-FIRMS 0
  set TOTAL-REVENUE 0
  set pringle-index-value 1
  set coats-index-value 1            
  set ussher-index-value 1              
  set potvin-index-value 1
    
  
  ;; setup agents
  
  create-households n-households [
    set shape "circle" 
    set ycor min-pycor + 1 + random 10
    set size 0.5
    set liquidity STARTUP-LIQUIDITY  
  ]
  
  setup-firms         
  initialize-framework-agreements
  
  
  set CONSUMER-GOOD-FIRMS firms with [consumer-good-firm?]
  set PRIMARY-GOOD-FIRMS firms with [primary-good-firm?]
  set INTERMEDIATE-GOOD-FIRMS firms with [intermediate-good-firm?]
  set LAST-TWO-YEARS-PRIMARY-INVENTORY (list (sum [inventory] of firms))
  set ALL-FIRM-PROFITS (list)
  set BANKRUPT-FIRM-PROFITS (list)
  set UNEMPLOYMENT-RATES (list)
  set MEAN-PRICES (list)
  
  
  ask households[
    set-household-xcor
    create-consumer-links-with up-to-n-of N-TRADING-LINKS firms with [consumer-good-firm?] [
      init-consumer-link
    ]
    if count my-employment-links = 0 [create-employment-link-with one-of firms [init-employment-link]]  ; households that didn't get employed when firms were created get employed
    set reservation-wage [wage-rate] of my-employer
    set-consumption             ; this has to be set after households have trading connections to calculate mean price
  ]

  ask firms [
    set liquidity STARTUP-LIQUIDITY
    set lifetime-profits liquidity * -1
    set all-positions-filled 0
    set demand max-production   ; demand initialized to be the intial output of the firm
    set desired-n-workers n-workers    
    set inventory 500
    set-input-demands   
    setup-equity-links
    set previous-sales max-production
    set average-previous-sales max-production
  ]
  
  
  set-colors
  ask one-of firms [ask my-links [show-link]]
  
  setup-patches
  
  reset-ticks
  tick
end

;procedure to initialize all global constants
to set-constants
  set MIN-WAGE-RATE 2.5
  set MAX-WAGE-GROWTH 0.019                
  set MONTHS-TO-LOWER-WAGE 1              
  set MAX-PRICE-GROWTH 0.05                 
  set BUFFER-LABOR-FRACTION 2               
  set DIMINISHING-UTILITY-CONSTANT 0.3     
  set PROB-REPLACE-FIRM-PRICE 0.25         
  set PROB-REPLACE-FIRM-QUANT 0.5          
  set N-TRADING-LINKS 7                    
  set N-FRAMEWORK-AGREEMENTS 7             
  set AUTONOMOUS-CONSUMPTION 0         
  set STARTUP-LIQUIDITY 100                
  set BACKGROUND-IMPROVEMENT 10
  set FIRM-MEMORY-CONSTANT 0.9
  
  (ifelse setup-structure = "single-firm" [ ;diminishing utility constant = 0.2-0.3 works well
    set FIRM-STRUCTURE table:get (table:from-json-file "Single-Firm-structure.json") "Firms"
  ] 
  setup-structure = "two-layer" [
    set FIRM-STRUCTURE table:get (table:from-json-file "Two-Layer-Firm-structure.json") "Firms"
  ]
  setup-structure = "three-layer" [
    set FIRM-STRUCTURE table:get (table:from-json-file "Three-Layer-Firm-structure.json") "Firms"
  ]
  setup-structure = "diamond" [
    set FIRM-STRUCTURE table:get (table:from-json-file "Diamond-Firm-structure.json") "Firms"
  ]
  setup-structure = "looped-diamond" [
    set FIRM-STRUCTURE table:get (table:from-json-file "Looped-Diamond-Firm-structure.json") "Firms"
  ])
  
end

;setup procedure for firms, creates every firm in the model according to the specifications in a json file
to setup-firms
  let firm-ratio-sum 0
  foreach FIRM-STRUCTURE[ f ->     ; we need to first find out how many firms to create
    set firm-ratio-sum firm-ratio-sum + table:get f "Firm count"  ; final sum should be 
  ]
  foreach FIRM-STRUCTURE[ f ->
    let group table:get f "Firm type"
    let firm-ratio table:get f "Firm count"
    let firm-tech-parameter table:get f "Tech constant"
    let is-consumer? table:get f "Consumer?"
    let is-intermediate? table:get f "Intermediate?"
    let is-primary? table:get f "Primary?"
    repeat ceiling ((firm-ratio / firm-ratio-sum) * n-firms) [
      let firm-input make-input-data-table table:get f "Input data"
      create-firms 1 [
        set shape "building store"
        set tech-parameter firm-tech-parameter 
        set price 1 * small-random-change
        set firm-type group
        set input-data firm-input
        set wage-rate tech-parameter * transactions-per-month 
        set consumer-good-firm? is-consumer?
        set intermediate-good-firm? is-intermediate?
        set primary-good-firm? is-primary?
        set fired-worker? false
        
        create-employment-link-with one-of households with [count my-employment-links = 0] [init-employment-link]
            
        set ycor 12 - random 5
        if table:length input-data != 0 [set ycor ycor - 9]
      ]
    ]
  ]
end

to setup-equity-links  ; firm procedure    
  let equity-per-employee 1 / count employment-link-neighbors
  let f self
  ask employment-link-neighbors [
    set liquidity liquidity - (equity-per-employee * [liquidity] of f)   ; this forces all liquidity to be initialized with households
    create-equity-link-with f [
      init-equity-link
      set equity equity-per-employee * [liquidity] of f    ;all employees should have equal amounts of equity-percent invested in the firm
    ]
  ]  
end

;generates the input data for a specific firm based on the structure specified in a json file
to-report make-input-data-table [original-data]
  ifelse length original-data = 0 [  ;;empty list means default settings of empty table
    report table:make
  ][
    let data-table table:make
    foreach original-data [i ->
      let inputi table:make
      table:put inputi "Marginal productivity" (table:get i "Marginal productivity")
      table:put inputi "Current stock" 30 * n-households / n-firms
      table:put inputi "Monthly demand" 0
      table:put data-table (table:get i "Input firm type") inputi
    ]
    report data-table
  ]
end

;creates a single firm's framework agreements to all necessary suppliers
to initialize-framework-agreements
  ask firms [
    foreach table:keys input-data [ i ->
      ask up-to-n-of N-FRAMEWORK-AGREEMENTS firms with [firm-type = i] [  ; some firms might not get a framework agreement using this procedure, but that is okay. they will be replaced if they can't make stuff
        create-framework-agreement-to myself [init-framework-agreement]        
      ]
    ]
  ]
end

to setup-patches
  ask firms [
    set firm-location (patch (((who - n-households) mod world-width) + min-pycor) (max-pxcor - (floor ((who - n-households) / world-width))))
    set soil-health random 50
    let patch-health soil-health
    ask firm-location [
      set pcolor scale-color black patch-health 0 200
    ]
  ]
end

to-report copy-table [ orig ]
  let copy table:make
  foreach ( table:keys orig ) [
    [key] -> table:put copy key ( table:get orig key )
  ]
  report copy
end