;;***************GO****************************
;general go procedure, runs the beginning of the month, then the month, then the end of the month
to go    
  
  go-beginning-of-month-observer  
  
  firms-plan-month
  
  go-beginning-of-month-households  
  
  ask firms [    
    FIRM.pay-wages
    FIRM.distribute-profits
  ]
  ask households [HOUSEHOLD.set-consumption]
  
  go-month  
  
  go-end-of-month
  tick
end

;egeneral beginning of the month procedure for resetting all model-wide varibales 
to go-beginning-of-month-observer 
  LAND.update-productive-capacity
  recalculate-index-pringle 
  recalculate-index-coats
  recalculate-index-ussher
  recalculate-index-potvin  
  set NEW-BANKRUPT-FIRMS 0
end

;general beginning of the month procedure for firms, see firm-procedures.nls for descriptions on each of these procedures
to firms-plan-month
  
  ask firms [ ; need to do this first so that new firms also plan month
    FIRM.set-color
    if liquidity < wage-rate [
      FIRM.replace-firm
    ]
  ]
  
  ask firms [
    
    FIRM.calculate-average-sales
    FIRM.search-for-framework-agreements        
    FIRM.adjust-wage-rate
    FIRM.adjust-output-and-price
    FIRM.lay-off-workers    
    set demand 0  ; reset demand for the month
    set previous-sales 0 ; reset how many goods were sold from the previous month     
    set month-production 0  ; reset how much was produced this month        
  ]
end

;general beginning of the month procedure for households, see household-procedures.nls for descriptions of each of these procedures
to go-beginning-of-month-households
  ask households [
    if liquidity < 0 [error "negative liquidity beginning of month"] 
    HOUSEHOLD.set-consumer-links                   
    HOUSEHOLD.search-for-employement   
    HOUSEHOLD.set-color  
  ]
    
end



;runs the daily production cycle for one month
to go-month
  repeat transactions-per-month [
    ask firms [
      ;check-framework-profitability   ; needs to occur after all frameworks are generated at beginning of month
      FIRM.buy-input-goods
      FIRM.produce-goods
    ]
    ask households [
      HOUSEHOLD.buy-consumption-goods
    ]
  ]
end

;general proccedure for the end of the month, see firm and household files for descriptions of each of thes procedures
to go-end-of-month
  if (sum [liquidity] of firms) + (sum [liquidity] of households) < 0.9999 * STARTUP-LIQUIDITY * n-households [error "liquidity is leaking"]
  if (sum [liquidity] of firms) + (sum [liquidity] of households) > 1.0001 * STARTUP-LIQUIDITY * n-households [error "liquidity is increasing"]              
  
  ask firms [
    if wage-rate < MIN-WAGE-RATE [error "firm has wage lower than policy minimum wage"]                           
    
    ifelse desired-n-workers <= n-workers [
      set months-without-vacancies months-without-vacancies + 1
    ] [
      set months-without-vacancies 0    ;if desired-n-workers > n-workers, then we failed to hire, i.e. had a vacancy
    ]
    
    set firm-age (firm-age + 1)    
  ]
  
  
  ask households [ 
    HOUSEHOLD.adjust-reservation-wage
  ]

  set ROLLING-UNEMPLOYMENT-RATE (.9 * ROLLING-UNEMPLOYMENT-RATE) + (.1 * (1 - (count employment-links / count households)))
  set-firm-xcor
  set-household-xcor  
  
  set month month + 1
  
end

