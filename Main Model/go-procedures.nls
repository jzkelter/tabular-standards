;;***************GO****************************
;general go procedure, runs the beginning of the month, then the month, then the end of the month
to go
  go-beginning-of-month-observer
  go-beginning-of-month-firms
  go-beginning-of-month-households
  go-month
  go-end-of-month
  tick
end

;egeneral beginning of the month procedure for resetting all model-wide varibales 
to go-beginning-of-month-observer
  recalculate-index-pringle 
  recalculate-index-coats
  recalculate-index-ussher
  recalculate-index-potvin
  update-rolling-mean-inventory
  set-colors
end

;general beginning of the month procedure for firms, see firm-procedures.nls for descriptions on each of these procedures
to go-beginning-of-month-firms
  ask firms [
    if (liquidity <= 0 ) and allow-firm-exit?[    ;;safe to assume that a firm that nobody shops at goes out of business
      remove-firm
    ]
    if input-data != "None"[
      set-daily-input-demands
    ]
    adjust-wage-rate
    adjust-labor-and-price
    set demand 0  ; reset demand for the month
    set previous-sales 0 ; reset how many goods were sold from the previous month
    if input-data != "None"[
      foreach table:keys input-data[i ->
        guarantee-framework-links i
        search-cheaper-supplier i
        search-delivery-capable-seller i
      ]
    ]
    pay-wages
  ]
end

;general beginning of the month procedure for households, see household-procedures.nls for descriptions of each of these procedures
to go-beginning-of-month-households
  ask households [
    if liquidity < 0 [set liquidity 0] ;we kept getting a rounding error where households would have a liquidity of 1.6 x 10 ^ -16 due to a rounding error. This corrects for that.
    guarantee-consumer-links
    if random-float 1 < prob-replace-firm-price  [search-cheaper-vendor]
    if random-float 1 < prob-replace-firm-quant [search-delivery-capable-vendor]  
    search-for-employement
    set-consumption
  ]
end

;runs the daily production cycle for one month
to go-month
  repeat transactions-per-month [
    ask firms [
      if input-data != "None"[
        buy-input-goods
      ]
      produce-goods
    ]
    ask households [
      buy-consumption-goods
    ]
  ]
end

;general proccedure for the end of the month, see firm and household files for descriptions of each of thes procedures
to go-end-of-month
  ask firms [
    distribute-profits
    (ifelse desired-n-workers < n-workers [
      set months-failed-to-hire months-failed-to-hire + 1
    ] [
      set months-failed-to-hire 0    
    ])
  ]
  ask households [
    adjust-reservation-wage
  ]
  ask firms [
    decide-fire-worker
  ] ; important for this to happen after wages are paid and households adjust since the model assumes one month between a firing decision and the person actually getting fired

  set-firm-xcor
  set-household-xcor

  set month month + 1
end



