;;***************GO****************************
;general go procedure, runs the beginning of the month, then the month, then the end of the month
to go  
  go-beginning-of-month-observer  
  go-beginning-of-month-firms  
  go-beginning-of-month-households  
  go-month  
  go-end-of-month
  tick
end

;egeneral beginning of the month procedure for resetting all model-wide varibales 
to go-beginning-of-month-observer  
  recalculate-index-pringle 
  recalculate-index-coats
  recalculate-index-ussher
  recalculate-index-potvin
  update-rolling-mean-inventory
  set-colors
end

;general beginning of the month procedure for firms, see firm-procedures.nls for descriptions on each of these procedures
to go-beginning-of-month-firms
  
  ask firms [
    update-soil-health
    
    if liquidity <= wage-rate [  
      remove-firm
    ]
    estimate-future-sales
    set-input-demands
    adjust-wage-rate
    adjust-labor-and-price
    set demand 0  ; reset demand for the month
    set previous-sales 0 ; reset how many goods were sold from the previous month  
    foreach table:keys input-data[i ->
      ;search-new-in-framework-agreements i
      guarantee-framework-links i
      ;search-cheaper-supplier i
      ;search-delivery-capable-seller i
    ]
    if not consumer-good-firm? [
      ;search-new-out-framework-agreements 
    ]
    pay-wages
  ]
end

;general beginning of the month procedure for households, see household-procedures.nls for descriptions of each of these procedures
to go-beginning-of-month-households
  ask households [
    if liquidity < 0 [error "negative liquidity beginning of month"] 
    guarantee-consumer-links
    if random-float 1 < PROB-REPLACE-FIRM-PRICE  [search-cheaper-vendor]
    if random-float 1 < PROB-REPLACE-FIRM-QUANT [search-delivery-capable-vendor]  
    search-for-employement
    set-consumption
  ]
end

;runs the daily production cycle for one month
to go-month
  repeat transactions-per-month [
    ask firms [
      ;check-framework-profitability   ; needs to occur after all frameworks are generated at beginning of month
      buy-input-goods
      produce-goods
    ]
    ask households [
      buy-consumption-goods
    ]
  ]
end

;general proccedure for the end of the month, see firm and household files for descriptions of each of thes procedures
to go-end-of-month
  if sum [liquidity] of turtles < 0.999 * STARTUP-LIQUIDITY * n-households [error "liquidity is leaking"]
  
  ask firms [
    if wage-rate < MIN-WAGE-RATE [error "firm has wage lower than policy minimum wage"]
    distribute-profits
    (ifelse desired-n-workers < n-workers [
      set all-positions-filled all-positions-filled + 1
    ] [
      set all-positions-filled 0    ;if desired-n-workers > n-workers, then we failed to hire and we should reset all-positions-filled
    ])
  ]
  ask households [ 
    adjust-reservation-wage
  ]
  
  set ALL-FIRM-PROFITS BANKRUPT-FIRM-PROFITS       ; reset all-firm-profits
  ask firms [
    set ALL-FIRM-PROFITS (lput lifetime-profits ALL-FIRM-PROFITS)
    decide-fire-worker
  ] ; important for this to happen after wages are paid and households adjust since the model assumes one month between a firing decision and the person actually getting fired

  set-firm-xcor
  set-household-xcor

  set UNEMPLOYMENT-RATES (lput (1 - count employment-links / n-households) UNEMPLOYMENT-RATES)
  set MEAN-PRICES (lput mean [price] of firms MEAN-PRICES)
  
  set month month + 1
  
end



