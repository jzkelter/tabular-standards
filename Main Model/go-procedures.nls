;;***************GO****************************
;general go procedure, runs the beginning of the month, then the month, then the end of the month
to go
  go-beginning-of-month-firms
  go-beginning-of-month-households
  go-month
  go-end-of-month
  tick
end

;general beginning of the month procedure for firms, see firm-procedures.nls for descriptions on each of these procedures
to go-beginning-of-month-firms
  ask firms [
    adjust-wage-rate
    adjust-job-positions
    adjust-price
    set-daily-input-demands
    set demand 0  ; reset demand for the month
    if input-data != "None"[
      foreach table:keys input-data[i ->
        search-cheaper-supplier i
        search-delivery-capable-seller i
      ]
    ]
  ]
end

;general beginning of the month procedure for households, see household-procedures.nls for descriptions of each of these procedures
to go-beginning-of-month-households
  
  ask households [
    if random-float 1 < prob-replace-firm-price  [search-cheaper-vendor]  ; with probability Ψprice, search for a cheaper vendor
    if random-float 1 < prob-replace-firm-quant [search-delivery-capable-vendor]  ; with probability Ψquant, search for a vendor that has ineventory
    search-for-employement
    set-consumption
  ]
end

;runs the daily production cycle for one month
to go-month
  ;;; during month
  repeat transactions-per-month [
    ask households [buy-consumption-goods]
    ask firms [
      produce-goods
      if input-data != "None"[
        buy-input-goods
      ]
    ]
  ]
end

;general proccedure for the end of the month, see firm and household files for descriptions of each of thes procedures
to go-end-of-month
  ;;variables declared here to prevent recalculation every time a new household is asked
  let liq-mean mean [liquidity] of households
  let liq-stdev standard-deviation [liquidity] of households
  let inv-mean mean [inventory] of firms
  let inv-stdev standard-deviation [inventory] of firms
  print liq-mean
  print liq-stdev
  ask firms [
    pay-wages
    allocate-profits
    set xcor 3 * (inventory - inv-mean) / inv-stdev
  ]
  distribute-profits
  ask households [
    adjust-reservation-wage
    ;set-house-size
    set xcor min (list max-pxcor (3 * (liquidity - liq-mean) / liq-stdev))
    ifelse unemployed? [
      set color red
    ] [
      set color blue
    ]
  ]
  ask firms [decide-fire-worker] ; important for this to happen after wages are paid and households adjust since the model assumes one month between a firing decision and the person actually getting fired

  set month month + 1
end



